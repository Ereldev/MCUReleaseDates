import com.android.build.gradle.internal.cxx.configure.gradleLocalProperties
import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget
import com.codingfeline.buildkonfig.compiler.FieldSpec.Type.STRING

plugins {
    kotlin("multiplatform")
    kotlin("plugin.serialization") version "1.5.20"
    id("com.codingfeline.buildkonfig")
    id("com.android.library")
    id("kotlin-parcelize")
    kotlin("kapt")
    jacoco
}

kotlin {
    android()

    val iosTarget: (String, KotlinNativeTarget.() -> Unit) -> KotlinNativeTarget = when {
        System.getenv("SDK_NAME")?.startsWith("iphoneos") == true -> ::iosArm64
        else -> ::iosX64
    }

    iosTarget("ios") {
        binaries {
            framework {
                baseName = "shared"
            }
        }
    }

    val ktorVersion = "1.6.1"
    val serializationVersion = "1.2.2"

    sourceSets {
        val commonMain by getting {
            dependencies {
                implementation("io.ktor:ktor-client-core:$ktorVersion")
                implementation("org.jetbrains.kotlinx:kotlinx-serialization-core:$serializationVersion")
                implementation("io.ktor:ktor-client-serialization:$ktorVersion")
            }
        }
        val commonTest by getting {
            dependencies {
                implementation(kotlin("test-common"))
                implementation(kotlin("test-annotations-common"))
                implementation("io.mockk:mockk-common:1.12.0")
            }
        }
        val androidMain by getting {
            dependencies {
                implementation("io.ktor:ktor-client-android:$ktorVersion")
            }
        }
        val androidTest by getting {
            dependencies {
                implementation(kotlin("test-junit"))
                implementation("junit:junit:4.13.2")
                implementation("io.mockk:mockk:1.12.0")
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1")
            }
        }
        val iosMain by getting {
            dependencies {
                implementation("io.ktor:ktor-client-ios:$ktorVersion")
            }
        }
        val iosTest by getting
    }
}

buildkonfig {
    packageName = "com.ereldev.mcureleasedates"

    defaultConfigs {
        val themoviedbApiKey: String = gradleLocalProperties(rootDir).getProperty("themoviedbApiKey")
        buildConfigField(STRING, "themoviedbApiKey", themoviedbApiKey)
    }
}

android {
    compileSdkVersion(30)
    sourceSets["main"].manifest.srcFile("src/androidMain/AndroidManifest.xml")
    defaultConfig {
        minSdkVersion(21)
        targetSdkVersion(30)
        testInstrumentationRunner = "org.junit.runner.Runner"
    }
}

jacoco {
    toolVersion = "0.8.7"
}

tasks.register<JacocoReport>("codeCoverageReport") {
    dependsOn("testDebugUnitTest")

    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."

    reports {
        html.isEnabled = true
        html.outputLocation.set(file("build/jacoco/html"))
    }

    classDirectories.setFrom(
        fileTree("${buildDir}/tmp/kotlin-classes/debug") {
            exclude("**/BuildKonfig.*") // Generated config
            exclude("**/*inlined*.*") // Generated by Kotlin
            exclude("**/model/**") // Ignore models data classes
        }
    )

    sourceDirectories.setFrom(files(
        "src/androidMain/kotlin",
        "src/commonMain/kotlin"
    ))

    executionData.setFrom(files("build/jacoco/testDebugUnitTest.exec"))
}
